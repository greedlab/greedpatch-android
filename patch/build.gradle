plugins {
    id 'com.jfrog.bintray' version '1.7'
    id "com.github.dcendents.android-maven" version "1.4.1"
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

//noinspection GroovyMissingReturnStatement,GroovyMissingReturnStatement
android {
    compileSdkVersion 24
    buildToolsVersion '24.0.0'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 24
        versionCode 1
        versionName '1.0'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.okio:okio:1.10.0'
}

version = PROJ_VERSION
group = PROJ_GROUP
archivesBaseName = PROJ_NAME

// -source.jar and -javadoc.jar

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// Signing Artifacts

signing {
    sign configurations.archives
}

// generate pom.xml for jcenter
install {
    repositories.mavenInstaller {
        pom.project {
            name 'greedpatch-android'
            packaging 'aar'
            groupId PROJ_GROUP
            artifactId PROJ_ARTIFACTID
            version PROJ_VERSION
            description PROJ_DESCRIPTION
            url 'https://github.com/greedlab/greedpatch-android'

            scm {
                connection 'scm:git:git@github.com:greedlab/greedpatch-android.git'
                developerConnection 'scm:git:git@github.com:greedlab/greedpatch-android.git'
                url 'https://github.com/greedlab/greedpatch-android'
            }

            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }

            developers {
                developer {
                    id 'bell'
                    name 'Bell Zhong'
                    email 'bell@greedlab.com'
                    organization 'greedlab'
                    organizationUrl 'http://www.greedlab.com'
                }
            }
        }
    }
}

// publish to jcenter
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    pkg {
        repo = 'greedpatch-android'
        name = PROJ_NAME
        userOrg = 'greedlab'
        websiteUrl = PROJ_WEBSITEURL
        vcsUrl = PROJ_VCSURL
        licenses = ['MIT']
        publish = true
        publicDownloadNumbers = true
        version {
            name = PROJ_VERSION
            desc = PROJ_DESCRIPTION
            released = new Date()
            vcsTag = '1.0'
        }
    }
}

// publish to mavenCentral
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: hasProperty('ossrhUsername')?ossrhUsername:'',
                               password: hasProperty('ossrhPassword')?ossrhPassword:'')
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: hasProperty('ossrhUsername')?ossrhUsername:'',
                               password: hasProperty('ossrhPassword')?ossrhPassword:'')
            }

            pom.project {
                name 'greedpatch-android'
                packaging 'aar'
                groupId PROJ_GROUP
                artifactId PROJ_ARTIFACTID
                version PROJ_VERSION
                description PROJ_DESCRIPTION
                url 'https://github.com/greedlab/greedpatch-android'

                scm {
                    connection 'scm:git:git@github.com:greedlab/greedpatch-android.git'
                    developerConnection 'scm:git:git@github.com:greedlab/greedpatch-android.git'
                    url 'https://github.com/greedlab/greedpatch-android'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'bell'
                        name 'Bell Zhong'
                        email 'bell@greedlab.com'
                        organization 'greedlab'
                        organizationUrl 'http://www.greedlab.com'
                    }
                }
            }
        }
    }
}
